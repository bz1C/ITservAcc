
#Если Сервер или ТолстыйКлиентОбычноеПриложение или ВнешнееСоединение Тогда
	
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.bz_Выработка.Записывать = Истина;
	Движения.bz_РаботыКВыставлению.Записывать = Истина;
	Движения.bz_СписанныеТрудозатраты.Записывать = Истина;
	
	
	Для Каждого стр Из Выработка Цикл
		Если стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Выставить или
			 стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Списать или
			 стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.ОтложитьССуммой 
		Тогда
				
			Движение = Движения.bz_РаботыКВыставлению.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.Проект = стр.Проект;
			Движение.Этап = стр.Этап;
			Движение.Работа = стр.Работа;
			Движение.Сотрудник = стр.Сотрудник;
			Движение.ПВР = стр.ПВР;
			Движение.КоличествоОтработано = стр.Количество;
			Движение.СуммаОтработано = стр.СуммаСписания;
			
		КонецЕсли;

		Если стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.ОтложитьССуммой Тогда 
			Движение = Движения.bz_РаботыКВыставлению.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.Проект = стр.Проект;
			Движение.Этап = стр.Этап;
			Движение.Работа = стр.Работа;
			Движение.Сотрудник = стр.Сотрудник;
			Движение.ПВР = стр.ПВР;
			Движение.КоличествоОтработано = стр.Количество;
			Движение.СуммаОтработано = стр.Сумма;
		КонецЕсли;
		
		Если стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Выставить Тогда
			Движение = Движения.bz_РаботыКВыставлению.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.Проект = стр.Проект;
			Движение.Этап = стр.Этап;
			Движение.Работа = стр.Работа;
			Движение.Сотрудник = стр.Сотрудник;
			Движение.ПВР = стр.ПВР;

			ДвижениеОб = Движения.bz_Выработка.Добавить();
			ДвижениеОб.Период = стр.Дата;
			ДвижениеОб.Организация = Организация;
			ДвижениеОб.Проект = стр.Проект;
			ДвижениеОб.Этап = стр.Этап;
			ДвижениеОб.Работа = стр.Работа;
			ДвижениеОб.Сотрудник = стр.Сотрудник;
			
			ДвижениеОб.ПВР = стр.ПВР;
			ДвижениеОб.Содержание = стр.Содержание;
			ДвижениеОб.Примечание = стр.Примечание;
			
			Если Согласован Тогда
				Движение.КоличествоСогласовано = стр.Количество;
				Движение.СуммаСогласовано = стр.Сумма;
				ДвижениеОб.ТрудозатратыСогласовано = стр.Количество;
				ДвижениеОб.СуммаСогласовано = стр.Сумма;
			Иначе
				Движение.КоличествоНаСогласовании = стр.Количество;
				Движение.СуммаНаСогласовании = стр.Сумма;
				ДвижениеОб.ТрудозатратыНаСогласовании = стр.Количество;
				ДвижениеОб.СуммаНаСогласовании = стр.Сумма;
			КонецЕсли;
		ИначеЕсли стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Списать Тогда
			ДвижениеОб = Движения.bz_СписанныеТрудозатраты.Добавить();
			ДвижениеОб.Период = стр.Дата;
			ДвижениеОб.Организация = Организация;
			ДвижениеОб.Проект = стр.Проект;
			ДвижениеОб.Этап = стр.Этап;
			ДвижениеОб.Работа = стр.Работа;
			ДвижениеОб.Сотрудник = стр.Сотрудник;
			ДвижениеОб.Причина = стр.Причина;
			ДвижениеОб.ПВР = стр.ПВР;
			ДвижениеОб.Комментарий = стр.Примечание;
			ДвижениеОб.Количество = стр.Количество;
			ДвижениеОб.Сумма = стр.Сумма;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Для Каждого стр Из Выработка Цикл
		Если стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Выставить и стр.Сумма = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru='Не заполнена сумма'"), 
				ЭтотОбъект, 
				СтрШаблон("Выработка[&1].Сумма",Формат("ЧН=; ЧГ=;", Выработка.Индекс(стр))), 
				"Объект", 
				Отказ);
		КонецЕсли;		
		Если стр.Количество = 0 и не стр.Действие.Пустая() и стр.Действие <> Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Списать Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru='Трудозатраты могут быть пустыми только в случае выбранного действия - Списание'"), 
				ЭтотОбъект, 
				СтрШаблон("Выработка[&1].Количество",Формат("ЧН=; ЧГ=;", Выработка.Индекс(стр))), 
				"Объект", 
				Отказ);
		КонецЕсли;
		Если стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Списать Тогда
			Если ПустаяСтрока(стр.Примечание) Тогда
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru='При списании трудозатрат нужно обязательно заполнить пояснение'"), 
					ЭтотОбъект, 
					СтрШаблон("Выработка[&1].Примечание",Формат("ЧН=; ЧГ=;", Выработка.Индекс(стр))), 
					"Объект", 
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
//		ИначеЕсли стр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Списать Тогда
////			Если стр.СуммаСписания = 0 и стр.Сумма = 0 Тогда
////				ОбщегоНазначения.СообщитьПользователю(
////					НСтр("ru='Нужно оценить стоимость списываемых трудозатрат'"), 
////					ЭтотОбъект, 
////					СтрШаблон("Выработка[&1].Сумма",Формат("ЧН=; ЧГ=;", Выработка.Индекс(стр))), 
////					"Объект", 
////					Отказ);
//			ИначеЕсли стр.СуммаСписания <> 0 и стр.Сумма <> 0 и стр.Сумма <> стр.СуммаСписания Тогда
//				ОбщегоНазначения.СообщитьПользователю(
//					НСтр("ru='Списываемая сумма не соответствует сумме выработки. "), 
//					ЭтотОбъект, 
//					СтрШаблон("Выработка[&1].Количество",Формат("ЧН=; ЧГ=;", Выработка.Индекс(стр))), 
//					"Объект", 
//					Отказ);
//				
//		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


#КонецЕсли