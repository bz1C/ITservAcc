
&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьСервер();
КонецПроцедуры     

&НаСервере
Процедура ЗаполнитьСервер()
	СостоянияЗакрытия = ПолучитьСостоянияЗакрытия();
	Объект.ВыработкаПрошлыхПериодов.Очистить();
	Объект.Выработка.Очистить();
	ЗаполнитьОстатки();
	ЗаполнитьТекМесяц();
КонецПроцедуры



&НаСервере
Функция ПолучитьСостоянияЗакрытия()
	Результат=РегистрыСведений.bz_СостоянияЗакрытияПериодов.ПолучитьПоследнее(Объект.Дата, Новый Структура("Проект", Объект.Проект));
	//Запрос=Новый Запрос;
	//Запрос.Текст=
	//"ВЫБРАТЬ
	//|	Т.Проект КАК Проект,
	//|	Т.Периодичность КАК Периодичность,
	//|	Т.Состояние КАК Состояние,
	//|	Т.ДатаДействияПо КАК ДатаДействияПо,
	//|	Т.Период КАК Период
	//|ИЗ
	//|	РегистрСведений.bz_СостоянияЗакрытияПериодов.СрезПоследних(&Дата, Проект = &Проект) КАК Т";
	//Запрос.УстановитьПараметр("Дата",);
	//Запрос.УстановитьПараметр("Периодичность",);
	//Результат=Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	Возврат Результат;
КонецФункции


&НаСервере
Процедура ЗаполнитьТекМесяц()
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	bz_Выработка.Этап КАК Этап,
	|	bz_Выработка.Работа КАК Работа,
	|	bz_Выработка.Сотрудник КАК Сотрудник,
	|	bz_Выработка.Работа.НеВыставляется КАК НеВыставляется,
	|	bz_Выработка.Трудозатраты КАК КоличествоОтработано,
	|	bz_Выработка.Содержание КАК Содержание,
	|	bz_Выработка.Примечание КАК Примечание,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(bz_Выработка.Работа.НеВыставляется, ЛОЖЬ) = ИСТИНА
	|			ТОГДА 0
	|		ИНАЧЕ bz_Выработка.Трудозатраты
	|	КОНЕЦ КАК КоличествоВыставлено,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(bz_Выработка.Работа.НеВыставляется, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА 0
	|		ИНАЧЕ bz_Выработка.Трудозатраты
	|	КОНЕЦ КАК КоличествоСписано,
	|	bz_Выработка.Проект.КонтрагентРаботодатель КАК КонтрагентРаботодатель,
	|	bz_Выработка.Период КАК Дата
	|ИЗ
	|	РегистрНакопления.bz_Выработка КАК bz_Выработка
	|ГДЕ
	|	bz_Выработка.Период МЕЖДУ &Дата1 И &Дата2
	|	И bz_Выработка.Активность = ИСТИНА
	|	И bz_Выработка.Организация = &Организация
	|	И bz_Выработка.Проект = &Проект";
	
	Запрос.УстановитьПараметр("Дата1", Перечисления.Периодичность.НачалоПериода(Объект.Дата, Объект.Периодичность, Перечисления.Периодичность.Месяц));
	Запрос.УстановитьПараметр("Дата2", Перечисления.Периодичность.КонецПериода(Объект.Дата, Объект.Периодичность, Перечисления.Периодичность.Месяц));
	Запрос.УстановитьПараметр("Проект", Объект.Проект);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
	Пока Выборка.Следующий() Цикл
		НовСтр = Объект.Выработка.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		Ставка = РегистрыСведений.bz_ПочасовыеСтавкиРабот.ПолучитьСтавку(Объект.Дата, Объект.Проект, Выборка.Работа, Выборка.КонтрагентРаботодатель);
		НовСтр.Сумма  = Ставка * Выборка.КоличествоОтработано;
	КонецЦикла;
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьОстатки()
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	bz_ВыставленныеРаботыОстатки.Этап КАК Этап,
	|	bz_ВыставленныеРаботыОстатки.Работа КАК Работа,
	|	bz_ВыставленныеРаботыОстатки.Сотрудник КАК Сотрудник,
	|	bz_ВыставленныеРаботыОстатки.КоличествоОтработаноОстаток КАК КоличествоОтработано,
	|	bz_ВыставленныеРаботыОстатки.СуммаОтработаноОстаток КАК Сумма,
	|	ПредыдущаяВыработкаТЧ.Содержание КАК Содержание,
	|	ПредыдущаяВыработкаТЧ.Примечание КАК Примечание
	|ИЗ
	|	РегистрНакопления.bz_ВыставленныеРаботы.Остатки(
	|			&Дата,
	|			Организация = &Организация
	|				И Проект = &Проект) КАК bz_ВыставленныеРаботыОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.bz_ВыставлениеРабот.Выработка КАК ПредыдущаяВыработкаТЧ
	|		ПО bz_ВыставленныеРаботыОстатки.Документ = ПредыдущаяВыработкаТЧ.Ссылка
	|			И bz_ВыставленныеРаботыОстатки.НомерСтр = ПредыдущаяВыработкаТЧ.НомерСтроки
	|ГДЕ
	|	(bz_ВыставленныеРаботыОстатки.КоличествоОтработаноОстаток <> 0
	|			ИЛИ bz_ВыставленныеРаботыОстатки.СуммаОтработаноОстаток <> 0)";
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Проект", Объект.Проект);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
	Пока Выборка.Следующий() Цикл    
		НовСтр = Объект.ВыработкаПрошлыхПериодов.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяВыработкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекСтр = Элементы.Выработка.ТекущиеДанные;
	Если НоваяСтрока или Копирование Тогда
		ТекСтр.ИзмененаВручную = Истина;
	КонецЕсли;  
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяВыработкаПриИзмененииРеквизитоыИзВыполненияРабот(Элемент)
	ТекСтр = Элементы.Выработка.ТекущиеДанные;
	ТекСтр.ИзмененаВручную = Истина;
КонецПроцедуры
