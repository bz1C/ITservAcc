#Область ОписаниеПеременных
&НаКлиенте
Перем констДействиеСписать, констДействиеОтложитьТолькоКоличество, констДействиеОтложитьССуммой, 
		констДействиеВыставитьБезВыработки;


#КонецОбласти


#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустая() Тогда
		Если Объект.Организация.Пустая() Тогда 
			ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
			Справочники.Организации.ПроверитьНаличиеОрганизацииПриОднофирменномУчете(ОсновнаяОрганизация);
			Объект.Организация = ОсновнаяОрганизация;
		КонецЕсли;
		
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	констДействиеСписать = ПредопределенноеЗначение("Перечисление.bz_ВариантыВыставленияОтработанногоВремени.Списать");
	констДействиеОтложитьТолькоКоличество = ПредопределенноеЗначение("Перечисление.bz_ВариантыВыставленияОтработанногоВремени.ОтложитьТолькоКоличество");
	констДействиеОтложитьССуммой = ПредопределенноеЗначение("Перечисление.bz_ВариантыВыставленияОтработанногоВремени.ОтложитьССуммой");
	констДействиеВыставитьБезВыработки = ПредопределенноеЗначение("Перечисление.bz_ВариантыВыставленияОтработанногоВремени.ВыставитьБезВыработки");
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
	&НаКлиенте
	Процедура ВыработкаПримечаниеПриИзменении(Элемент)
		УстановитьФлагТекСтроки("СодержаниеИзменено");
	КонецПроцедуры

	&НаКлиенте
	Процедура ВыработкаСодержаниеПриИзменении(Элемент)
		УстановитьФлагТекСтроки("СодержаниеИзменено");
	КонецПроцедуры

	&НаКлиенте
	Процедура ПроектПриИзменении(Элемент)
		УправлениеФормой(ЭтотОбъект);
	КонецПроцедуры

	&НаКлиенте
	Процедура КонтрагентПриИзменении(Элемент)
		УправлениеФормой(ЭтотОбъект);
	КонецПроцедуры

	&НаКлиенте
	Процедура МенеджерПриИзменении(Элемент)
		УправлениеФормой(ЭтотОбъект);
	КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТекущаяВыработка

	&НаКлиенте
	Процедура ТекущаяВыработкаКоличествоПриИзменении(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		ТекСтр.ИзмененаВручную = Истина;
		ТекСтр.Сумма = ТекСтр.Цена*ТекСтр.Количество;
		ТекСтр.СуммаСписания = ТекСтр.ЦенаСписания*ТекСтр.Количество; 
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаПриИзмененииРеквизитовИзВыполненияРабот(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		ТекСтр.ИзмененаВручную = Истина;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		Если НоваяСтрока или Копирование Тогда
			ТекСтр.ИзмененаВручную = Истина;
		КонецЕсли;  
		
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаПримечаниеПриИзменении(Элемент)
		УстановитьФлагТекСтроки("СодержаниеИзменено");
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаСодержаниеПриИзменении(Элемент)
		УстановитьФлагТекСтроки("СодержаниеИзменено");
	КонецПроцедуры


	&НаКлиенте
	Процедура ТекущаяВыработкаДействиеПриИзменении(Элемент)
		УстановитьВидимостьКолонокТЧ();
		
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		Если ТекСтр = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если не ТекСтр.Причина.Пустая() и ТекСтр.Действие <> констДействиеСписать Тогда
			ТекСтр.Причина = Неопределено;
		КонецЕсли;
		Если (ТекСтр.Действие = констДействиеОтложитьТолькоКоличество 
				или ТекСтр.Действие = констДействиеОтложитьССуммой)
				и ТекСтр.ДатаПереноса = '00010101'
		Тогда
			ТекСтр.ДатаПереноса = КонецМесяца(Объект.Дата)+1;					
		КонецЕсли;
		
		Если ТекСтр.Действие = констДействиеВыставитьБезВыработки Тогда
			ТекСтр.ПВР = Неопределено;
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаПричинаПриИзменении(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		Если ТекСтр <> Неопределено и ТекСтр.СодержаниеИзменено = Ложь 
			и ТекСтр.Действие = констДействиеСписать
			И не ТекСтр.Причина.Пустая() 
		Тогда
			ТекСтр.Содержание = "";
		КонецЕсли;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаЦенаПриИзменении(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		ТекСтр.ИзмененаВручную = Истина;
		ТекСтр.Сумма = ТекСтр.Цена*ТекСтр.Количество;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаЦенаСписанияПриИзменении(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		ТекСтр.СписатьПоДругойЦене = Истина;
		ТекСтр.СуммаСписания = ТекСтр.ЦенаСписания*ТекСтр.Количество;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаСуммаСписанияПриИзменении(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		ТекСтр.СписатьПоДругойЦене = Истина;
		Если ТекСтр.Количество = 0 Тогда
			ТекСтр.ЦенаСписания = ТекСтр.СуммаСписания;
		Иначе
			ТекСтр.ЦенаСписания = ТекСтр.СуммаСписания / ТекСтр.Количество;
		КонецЕсли;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаСуммаПриИзменении(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		ТекСтр.ИзмененаВручную = Истина;
		Если ТекСтр.Количество = 0 Тогда
			ТекСтр.Цена = ТекСтр.Сумма;
		Иначе
			ТекСтр.Цена = ТекСтр.Сумма / ТекСтр.Количество;
		КонецЕсли;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТекущаяВыработкаПриАктивизацииСтроки(Элемент)
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		Если ТекСтр = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.ВыработкаСодержание.Видимость = ТекСтр.Действие <> констДействиеСписать;
	КонецПроцедуры



#КонецОбласти



#Область ОбработчикиКомандФормы
&НаКлиенте
Асинх Процедура Заполнить(Команда)
	Заголовок = НСтр("ru='Заполнение табличной части'"); 
	Если Объект.Проведен Тогда
		ПоказатьПредупреждение(,НСтр("ru='Проведенный документ нельзя заполнять.'"), 0, Заголовок);
		Возврат;
	КонецЕсли;
	Если Объект.Выработка.Количество() Тогда
		Ответ = Ждать ВопросАсинх(
			Нстр("ru='Табличная часть содержит строки и будет очищена. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет,,
			КодВозвратаДиалога.Нет,
			Заголовок
		);
			
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
		БылиРучныеИзменения = Ложь;
		Для Каждого стр Из Объект.Выработка Цикл
			Если стр.ИзмененаВручную или стр.СодержаниеИзменено или стр.Действие = констДействиеВыставитьБезВыработки Тогда 
				БылиРучныеИзменения = Истина;
				Прервать;
			КонецЕсли; 			
		КонецЦикла;
		
		Если БылиРучныеИзменения Тогда
			Ответ = Ждать ВопросАсинх(
				Нстр("ru='Табличная часть содержит строки, измененные вручную. Все равно продолжить?'"),
				РежимДиалогаВопрос.ДаНет, 
				5,
				КодВозвратаДиалога.Нет,
				Заголовок,
				КодВозвратаДиалога.Нет
			);
				
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
			
		Объект.Выработка.Очистить();
	КонецЕсли;
	
	ЗаполнитьСервер();
КонецПроцедуры     


&НаКлиенте
Процедура ЗаполнитьВыбранныеТекущейСтавкой(Команда)
	ЗаполнитьВыбранныеТекущейСтавкойНаСервере();
КонецПроцедуры



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

	&НаКлиенте
	Процедура УстановитьВидимостьКолонокТЧ()	
		ЕстьСписание = Ложь;
		Для Каждого стр Из Объект.Выработка Цикл
			Если стр.Действие = констДействиеСписать Тогда
				ЕстьСписание = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Элементы.ТекущаяВыработкаПричина.Видимость = ЕстьСписание;
			
	КонецПроцедуры

	&НаСервере
	Процедура ЗаполнитьСервер()
		Объект.Выработка.Очистить();
		ЗаполнитьТЧВыработка();
	КонецПроцедуры

	&НаКлиенте
	Процедура УстановитьФлагТекСтроки(ИмяФлага)
		Перем ТекСтр;
		ТекСтр = Элементы.ТекущаяВыработка.ТекущиеДанные;
		Если ТекСтр <> Неопределено Тогда
			ТекСтр[ИмяФлага] = Истина;
		КонецЕсли;
	КонецПроцедуры

	&НаСервере
	Процедура ЗаполнитьВыбранныеТекущейСтавкойНаСервере()
		ВыбранныеСтроки = Элементы.ТекущаяВыработка.ВыделенныеСтроки;
		
		Для Каждого ИдВыделеннойСтроки Из ВыбранныеСтроки Цикл
			ТекСтр = Объект.Выработка.НайтиПоИдентификатору(ИдВыделеннойСтроки);
			Если ТекСтр = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Ставка = РегистрыСведений.bz_ПочасовыеСтавкиРабот.ПолучитьСтавку(Объект.Дата, ТекСтр.Проект, ТекСтр.Работа);
			ТекСтр.Цена = Ставка;
			ТекСтр.Сумма = ТекСтр.Цена * ТекСтр.Количество; 
		КонецЦикла;
	КонецПроцедуры

	&НаСервере
	Процедура ЗаполнитьТЧВыработка()
		Запрос=Новый Запрос;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	bz_РаботыКВыставлениюОстатки.Проект КАК Проект,
		|	bz_РаботыКВыставлениюОстатки.Этап КАК Этап,
		|	bz_РаботыКВыставлениюОстатки.Работа КАК Работа,
		|	bz_РаботыКВыставлениюОстатки.Сотрудник КАК Сотрудник,
		|	bz_РаботыКВыставлениюОстатки.ПВР КАК ПВР,
		|	ЕСТЬNULL(bz_РаботыКВыставлениюОстатки.Работа.НеВыставляется, ЛОЖЬ) КАК НеВыставляется,
		|	bz_РаботыКВыставлениюОстатки.КоличествоОтработаноОстаток КАК Количество,
		|	bz_РаботыКВыставлениюОстатки.СуммаОтработаноОстаток КАК СуммаСписания,
		|	ЕСТЬNULL(bz_РаботыКВыставлениюОстатки.ПВР.Содержание, """") КАК Содержание,
		|	ЕСТЬNULL(bz_РаботыКВыставлениюОстатки.ПВР.Примечание, """") КАК Примечание,
		|	ЕСТЬNULL(bz_РаботыКВыставлениюОстатки.ПВР.Дата, &ДатаДокумента) КАК Дата,
		|	bz_РаботыКВыставлениюОстатки.Проект.КонтрагентРаботодатель КАК КонтрагентРаботодатель
		|ИЗ
		|	РегистрНакопления.bz_РаботыКВыставлению.Остатки(&Дата, Организация = &Организация
		|	И Проект = &Проект) КАК bz_РаботыКВыставлениюОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Сотрудник,
		|	КонтрагентРаботодатель,
		|	Проект,
		|	Этап,
		|	Работа";
		
		Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата)+1);
		Запрос.УстановитьПараметр("ДатаДокумента", НачалоДня(Объект.Дата));
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
		Запрос.УстановитьПараметр("Менеджер", Объект.Менеджер);
		
		
		Если не Объект.Проект.Пустая() Тогда
			Запрос.УстановитьПараметр("Проект", Объект.Проект);
		ИначеЕсли не Объект.Контрагент.Пустая() и не Объект.Менеджер.Пустая() Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Проект = &Проект", "И Проект В (ВЫБРАТЬ П.Ссылка ИЗ Справочник.bz_Проекты КАК П ГДЕ П.КонтрагентРаботодатель = &Контрагент И П.Менеджер = &Менеджер)");
		ИначеЕсли не Объект.Контрагент.Пустая() Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Проект = &Проект", "И Проект В (ВЫБРАТЬ П.Ссылка ИЗ Справочник.bz_Проекты КАК П ГДЕ П.КонтрагентРаботодатель = &Контрагент)");
		ИначеЕсли не Объект.Менеджер.Пустая() Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Проект = &Проект", "И Проект В (ВЫБРАТЬ П.Ссылка ИЗ Справочник.bz_Проекты КАК П ГДЕ П.Менеджер = &Менеджер)");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Проект = &Проект", "");
		КонецЕсли;
		
		Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока Выборка.Следующий() Цикл
			НовСтр = Объект.Выработка.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
			НовСтр.ЦенаСписания = ?(Выборка.Количество=0, Выборка.СуммаСписания, Выборка.СуммаСписания/Выборка.Количество);
			
			Если Выборка.НеВыставляется Тогда			
				НовСтр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Списать;
				Если ПустаяСтрока(НовСтр.Примечание) и не ПустаяСтрока(НовСтр.Содержание) Тогда
					НовСтр.Примечание = НовСтр.Содержание;
				КонецЕсли; 
			Иначе
				НовСтр.Действие = Перечисления.bz_ВариантыВыставленияОтработанногоВремени.Выставить;
			КонецЕсли;
			
			Если Выборка.СуммаСписания = 0 Тогда
				Ставка = РегистрыСведений.bz_ПочасовыеСтавкиРабот.ПолучитьСтавку(Выборка.Дата, Выборка.Проект, Выборка.Работа, Выборка.КонтрагентРаботодатель);
				НовСтр.Цена = Ставка;
			Иначе
				НовСтр.Цена = НовСтр.ЦенаСписания;
			КонецЕсли;
			
			НовСтр.Сумма = НовСтр.Цена * НовСтр.Количество;
		КонецЦикла;
	КонецПроцедуры

	&НаКлиентеНаСервереБезКонтекста
	Процедура УправлениеФормой(Форма)
		Элементы = Форма.Элементы;
		Объект = Форма.Объект;
		
		Элементы.Проект.Видимость = не Объект.Проект.Пустая() или Объект.Менеджер.Пустая() и Объект.Контрагент.Пустая();
		Элементы.Менеджер.Видимость = Объект.Проект.Пустая();
		Элементы.Контрагент.Видимость = Объект.Проект.Пустая();
	КонецПроцедуры

#КонецОбласти





























