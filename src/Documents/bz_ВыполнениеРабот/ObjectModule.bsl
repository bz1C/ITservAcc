
#Если Сервер или ТолстыйКлиентОбычноеПриложение или ВнешнееСоединение Тогда  
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Трудозатраты = Работы.Итог("ТрудозатратыЧ") + Работы.Итог("ТрудозатратыМ")/60;
	
	ЕдинственныйПроект = Неопределено;
	Для каждого стр Из Работы Цикл
		Если ЕдинственныйПроект = Неопределено Тогда
			ЕдинственныйПроект = стр.Проект;		
		ИначеЕсли ЕдинственныйПроект <> стр.Проект Тогда
			ЕдинственныйПроект = Неопределено;
			Прервать;
		КонецЕсли;		
	КонецЦикла;
	Если ЕдинственныйПроект <> Неопределено Тогда
		Проект = ЕдинственныйПроект;
		ЕдинственныйЭтап = Неопределено;
		Для каждого стр Из Работы Цикл
			Если ЕдинственныйЭтап = Неопределено Тогда
				ЕдинственныйЭтап = стр.Этап;		
			ИначеЕсли ЕдинственныйЭтап <> стр.Этап Тогда
				ЕдинственныйЭтап = Неопределено;
				Прервать;
			КонецЕсли;		
		КонецЦикла;
		Если ЕдинственныйЭтап <> Неопределено Тогда
			Этап = ЕдинственныйЭтап;
		КонецЕсли;		
	КонецЕсли;
	
	
	ОбновитьПВРСтрок();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	МасПроектов = Новый Массив;
	
	// Формирование движений 
	Движения.bz_Выработка.Записывать = Истина;
	Движения.bz_РаботыКВыставлению.Записывать = Истина;
	Для Каждого ТекСтрокаРаботы Из Работы Цикл
		Движение = Движения.bz_Выработка.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Проект = ТекСтрокаРаботы.Проект;
		Движение.Этап = ТекСтрокаРаботы.Этап;
		Движение.Работа = ТекСтрокаРаботы.Работа;
		Движение.Сотрудник = Сотрудник;
		Движение.ТрудозатратыЧД = ТекСтрокаРаботы.ТрудозатратыЧ;
		Движение.ТрудозатратыМД = ТекСтрокаРаботы.ТрудозатратыМ;
		Движение.Трудозатраты = ТекСтрокаРаботы.ТрудозатратыЧ + ТекСтрокаРаботы.ТрудозатратыМ/60;
		Движение.ТрудозатратыМинуты = Окр(ТекСтрокаРаботы.ТрудозатратыМ+ТекСтрокаРаботы.ТрудозатратыЧ*60,0);
		Движение.Содержание = ТекСтрокаРаботы.Содержание;
		Движение.Примечание = ТекСтрокаРаботы.Примечание;
		Движение.ПВР = ТекСтрокаРаботы.ПВРСтроки;


		ДвижениеРВ = Движения.bz_РаботыКВыставлению.Добавить();
		ДвижениеРВ.Период = Дата;
		ЗаполнитьЗначенияСвойств(ДвижениеРВ, Движение);
		
		ДвижениеРВ.КоличествоОтработано = Движение.Трудозатраты;
		
		Если ЗначениеЗаполнено(ТекСтрокаРаботы.Проект) Тогда
			МасПроектов.Добавить(ТекСтрокаРаботы.Проект);
		КонецЕсли;
	КонецЦикла;
	
	МасПроектов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МасПроектов);
	ПроверитьСоздатьСостояниеЗакрытияПериодаПоПроектам(МасПроектов, Отказ);
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	bz_СостоянияЗакрытияПериодов.Состояние КАК Состояние,
	|	bz_СостоянияЗакрытияПериодов.Период КАК Период,
	|	bz_СостоянияЗакрытияПериодов.ДатаДействияПо КАК ДатаДействияПо,
	|	bz_СостоянияЗакрытияПериодов.Проект КАК Проект
	|ИЗ
	|	РегистрСведений.bz_СостоянияЗакрытияПериодов КАК bz_СостоянияЗакрытияПериодов
	|ГДЕ
	|	bz_СостоянияЗакрытияПериодов.Проект В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Т.Проект КАК Проект
	|			ИЗ
	|				Документ.bz_ВыполнениеРабот.Работы КАК Т
	|			ГДЕ
	|				Т.Ссылка = &Ссылка)
	|	И bz_СостоянияЗакрытияПериодов.Период <= &Период
	|	И bz_СостоянияЗакрытияПериодов.ДатаДействияПо >= &Период";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", НачалоДня(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"Дата" , , )));
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Состояние) и Выборка.Состояние <> Перечисления.bz_СостоянияПроектовВПериодах.Открыт Тогда
			ТекстСообщения = СтрШаблон(
			 	НСтр("ru='Период %1 по проекту %2 %3. Для отмены выработки необходимо его открыть.'"),
			 	ПредставлениеПериода(Выборка.Период, КонецДня(Выборка.ДатаДействияПо)),
				Строка(Выборка.Проект),
				Строка(Выборка.Состояние)
			 );
			 
			 Сообщение = Новый СообщениеПользователю;
			 Сообщение.Текст 		= ТекстСообщения;
			 Сообщение.Поле 			= "Дата";
			 Сообщение.ПутьКДанным 	= "Объект";
			 Сообщение.Сообщить();
			 Отказ = Истина;
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ПроверитьСоздатьСостояниеЗакрытияПериодаПоПроектам(МасПроектов, Отказ)
	Перем СостояниеЗакрытияПериода;
	Перем НачалоПериода;
	Перем Сообщение;
	Перем КонецПериода;
	Перем ТЗСостоянияЗакрытияПериодов;
	Перем ТекстСообщения;
	Перем Запрос;
	Перем З;
	Перем Выборка;
	Перем НЗ;
	
	Если Отказ = Истина или МасПроектов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Т.Период КАК Период,
	|	Т.Проект КАК Проект,
	|	Т.Состояние КАК Состояние,
	|	Т.ДатаДействияПо КАК ДатаДействияПо,
	|	Т.Периодичность КАК Периодичность
	|ИЗ
	|	РегистрСведений.bz_СостоянияЗакрытияПериодов.СрезПоследних(&Дата, Проект В (&Проекты)) КАК Т";
	
	
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("Проекты", МасПроектов);
	
	ТЗСостоянияЗакрытияПериодов=Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	//ТЗСостоянияЗакрытияПериодов = РегистрыСведений.bz_СостоянияЗакрытияПериодов.СрезПоследних(КонецДня(Дата), Новый Структура("Проект", МасПроектов));
	ТЗСостоянияЗакрытияПериодов.Индексы.Добавить("Проект");
	Для каждого Проект Из МасПроектов Цикл
		Если Отказ = Истина Тогда
			Прервать;
		КонецЕсли;
		СостояниеЗакрытияПериода = ТЗСостоянияЗакрытияПериодов.Найти(Проект, "Проект");
		Если СостояниеЗакрытияПериода = Неопределено Тогда
			НЗ = РегистрыСведений.bz_СостоянияЗакрытияПериодов.СоздатьНаборЗаписей();
			НЗ.Отбор.Проект.Установить(Проект);
			З = НЗ.Добавить();
			З.Период = НачалоМесяца(Дата);
			З.Проект = Проект;
			З.ДатаДействияПо = КонецМесяца(Дата);
			З.Состояние = Перечисления.bz_СостоянияПроектовВПериодах.Открыт;
			З.Периодичность = Перечисления.Периодичность.Месяц;
			НЗ.Записать(Ложь);
		Иначе
			Если Дата >= СостояниеЗакрытияПериода.Период и Дата <= СостояниеЗакрытияПериода.ДатаДействияПо Тогда
				// нашли настройку периода, внутри которого находимся
				Если не ЗначениеЗаполнено(СостояниеЗакрытияПериода.Состояние) или СостояниеЗакрытияПериода.Состояние = Перечисления.bz_СостоянияПроектовВПериодах.Открыт Тогда
					;
					// период открыт, отражение выработки возможно.
				Иначе
					ТекстСообщения = СтрШаблон(
					 	НСтр("ru='В периоде %1 проект %2 %3. Для отражения трудозатрат необходимо его открыть.'"),
					 	ПредставлениеПериода(СостояниеЗакрытияПериода.Период, КонецДня(СостояниеЗакрытияПериода.ДатаДействияПо)),
						Строка(Проект),
						СостояниеЗакрытияПериода.Состояние
					 );
					 
					 Сообщение = Новый СообщениеПользователю;
					 Сообщение.Текст 		= ТекстСообщения;
					 Сообщение.Поле 			= "Дата";
					 Сообщение.ПутьКДанным 	= "Объект";
					 Сообщение.Сообщить();
					 Отказ = Истина;
				КонецЕсли;
			Иначе
				// Дата > СостояниеЗакрытияПериода.ДатаДействияПо
				НачалоПериода = Перечисления.Периодичность.НачалоПериода(Дата, СостояниеЗакрытияПериода.Периодичность, Перечисления.Периодичность.Месяц);
				КонецПериода = Перечисления.Периодичность.КонецПериода(Дата, СостояниеЗакрытияПериода.Периодичность, Перечисления.Периодичность.Месяц);
				
				Запрос=Новый Запрос;
				Запрос.Текст=
				"ВЫБРАТЬ
				|	bz_СостоянияЗакрытияПериодов.Проект КАК Проект,
				|	bz_СостоянияЗакрытияПериодов.Период КАК Период,
				|	bz_СостоянияЗакрытияПериодов.ДатаДействияПо КАК ДатаДействияПо
				|ИЗ
				|	РегистрСведений.bz_СостоянияЗакрытияПериодов КАК bz_СостоянияЗакрытияПериодов
				|ГДЕ
				|	bz_СостоянияЗакрытияПериодов.Проект = &Проект
				|	И (bz_СостоянияЗакрытияПериодов.Период МЕЖДУ &Период1 И &Период2
				|			ИЛИ bz_СостоянияЗакрытияПериодов.ДатаДействияПо МЕЖДУ &Период1 И &Период2)";
				Запрос.УстановитьПараметр("Проект", Проект);
				Запрос.УстановитьПараметр("Период1", НачалоПериода);
				Запрос.УстановитьПараметр("Период2", КонецПериода);
				Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
				Пока Выборка.Следующий() Цикл
					ТекстСообщения = СтрШаблон(
					 	НСтр("ru='Необходимо открыть для списания трудозатрат период %1 по проекту %2, но он пересекается с периодом %3. Автоматическое открытие невозможно. Откройте данный период вручную.'"),
					 	ПредставлениеПериода(НачалоПериода, КонецПериода),
						Строка(Проект),
						ПредставлениеПериода(Выборка.Период, КонецДня(Выборка.ДатаДействияПо))
					 );
					 
					 Сообщение = Новый СообщениеПользователю;
					 Сообщение.Текст 		= ТекстСообщения;
					 Сообщение.Поле 			= "Дата";
					 Сообщение.ПутьКДанным 	= "Объект";
					 Сообщение.Сообщить();
					 Отказ = Истина;
					
				 КонецЦикла;
				 
				 Если Отказ = Ложь Тогда
					НЗ = РегистрыСведений.bz_СостоянияЗакрытияПериодов.СоздатьНаборЗаписей();
					НЗ.Отбор.Проект.Установить(Проект);
					З = НЗ.Добавить();
					З.Проект = Проект;
					З.Период = НачалоПериода;
					З.ДатаДействияПо = КонецПериода;
					З.Состояние = Перечисления.bz_СостоянияПроектовВПериодах.Открыт;
					З.Периодичность = СостояниеЗакрытияПериода.Периодичность;
					НЗ.Записать(Ложь);
				 КонецЕсли;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры


Процедура ОбновитьПВРСтрок()
	КолонкиПоискаСоответствияПВР = "Содержание, Примечание";
	структКолонкиПоискаСоответствияПВР = Новый	Структура(КолонкиПоискаСоответствияПВР);
	
	Если ЭтоНовый() Тогда
		СсылкаДляЗаписи = ПолучитьСсылкуНового();
		Если СсылкаДляЗаписи.Пустая() Тогда 
			СсылкаДляЗаписи = Документы.bz_ВыполнениеРабот.ПолучитьСсылку(Новый УникальныйИдентификатор());
			УстановитьСсылкуНового(СсылкаДляЗаписи);
		КонецЕсли;
	Иначе
		СсылкаДляЗаписи = Ссылка;	
	КонецЕсли;
	
	// Готовим таблицу строк для обхода
	РаботыДляЗаполненияПВР = Работы.Выгрузить(,КолонкиПоискаСоответствияПВР+", НомерСтроки, ПВРСтроки");
	отБулево = Новый ОписаниеТипов("Булево");
	РаботыДляЗаполненияПВР.Колонки.Добавить("ПВРПустая", отБулево);
	РаботыДляЗаполненияПВР.Колонки.Добавить("ПВРСоответствуетСтроке", отБулево);
	
	МасПВРДляПолученияДанных = Новый Массив();
	// НеиспользованныеПВР используем сначала для исключения дублей в МасПВРДляПолученияДанных и хранения всех значений ПВРСтроки,
	// а потом, после ремеппинга в ней останутся только неиспользованные значения. 
	НеиспользованныеПВР = Новый Соответствие();
	Для Каждого стр Из РаботыДляЗаполненияПВР Цикл
		Если стр.ПВРСтроки.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		Если НеиспользованныеПВР[стр.ПВРСтроки] <> Истина Тогда		
			МасПВРДляПолученияДанных.Добавить(стр.ПВРСтроки);
			НеиспользованныеПВР.Вставить(стр.ПВРСтроки, Истина);
		КонецЕсли;
	КонецЦикла;
	//МасПВРДляПолученияДанных = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МасПВРДляПолученияДанных);
	Если МасПВРДляПолученияДанных.Количество() Тогда
		ДанныеПВР = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МасПВРДляПолученияДанных, КолонкиПоискаСоответствияПВР + ", ВыполнениеРабот");
	Иначе
		ДанныеПВР = Новый Соответствие();
	КонецЕсли;
	Для Каждого стр Из РаботыДляЗаполненияПВР Цикл
		стр.ПВРПустая = стр.ПВРСтроки.Пустая();		
		Если стр.ПВРПустая Тогда
			стр.ПВРСоответствуетСтроке = Ложь;		 			
		Иначе
			ДанныеПВРСтроки = ДанныеПВР[стр.ПВРСтроки];
			КлючевыеКолонкиСовпадают = Истина;
			Для Каждого Рекв Из ДанныеПВРСтроки Цикл
				Если Рекв.Ключ = "ВыполнениеРабот" Тогда
					Если Рекв.Значение <> СсылкаДляЗаписи Тогда
						КлючевыеКолонкиСовпадают = Ложь;
					КонецЕсли;
				ИначеЕсли Рекв.Значение <> стр[Рекв.Ключ] Тогда
					 КлючевыеКолонкиСовпадают = Ложь;
					 Прервать;
				КонецЕсли;
			КонецЦикла;
			стр.ПВРСоответствуетСтроке = КлючевыеКолонкиСовпадают;		 
		КонецЕсли;
	КонецЦикла;
			
	РаботыДляЗаполненияПВР.Сортировать(КолонкиПоискаСоответствияПВР+", ПВРПустая, ПВРСоответствуетСтроке Убыв");
	
	// Произведем ремеппинг.
	// Обходим подготовленную таблицу и генерим/подставляем ПВР для всех строк
	// Менять значения в существующих (кроме даты) нельзя, поскольку это может повлиять на результат в регистрах
	ИспользованныеПВР = Новый Соответствие();
	ПредСтр = Неопределено;
	Для Каждого стр Из РаботыДляЗаполненияПВР Цикл
		Если ПредСтр <> Неопределено Тогда			
			КлючевыеКолонкиСовпадают = Истина;
			Для Каждого Рекв Из структКолонкиПоискаСоответствияПВР Цикл
				Если ПредСтр[Рекв.Ключ] <> стр[Рекв.Ключ] Тогда
					 КлючевыеКолонкиСовпадают = Ложь;
					 Прервать;
				КонецЕсли;
			КонецЦикла;

			Если КлючевыеКолонкиСовпадают Тогда
				стр.ПВРСтроки = ПредСтр.ПВРСтроки;
				ПредСтр = стр;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если стр.ПВРПустая Тогда
			стр.ПВРСтроки = СоздатьПВРСтроки(СсылкаДляЗаписи, Дата, стр, КолонкиПоискаСоответствияПВР);
		ИначеЕсли не стр.ПВРСоответствуетСтроке Тогда 
			стр.ПВРСтроки = СоздатьПВРСтроки(СсылкаДляЗаписи, Дата, стр, КолонкиПоискаСоответствияПВР);
		Иначе
			НеиспользованныеПВР.Удалить(стр.ПВРСтроки);
			ИспользованныеПВР.Вставить(стр.ПВРСтроки);			
		КонецЕсли;
		ПредСтр = стр;
	КонецЦикла;
	
	
	// теперь в НеиспользованныеПВР остались только те ПВР, которые более не используются в документе. Пометим их на удаление.
	Для Каждого ПВРУдаление Из НеиспользованныеПВР Цикл
		ПВРУдалениеОб = ПВРУдаление.Ключ.ПолучитьОбъект();
		ПВРУдалениеОб.УстановитьПометкуУдаления(Истина);						
	КонецЦикла;
	
	// Если поменялась дата, то в старых элементах её нужно обновить
	Если не ЭтоНовый() и НачалоДня(Дата) <> НачалоДня(Ссылка.Дата) Тогда
		Для Каждого ПВР Из ИспользованныеПВР Цикл			
			ПВРОб = ПВР.Ключ.ПолучитьОбъект();
			Если НачалоДня(Дата) <> ПВРОб.Дата Тогда
				ПВРОб.Дата = НачалоДня(Дата);
				ПВРОб.Записать();				
			КонецЕсли; 									
		КонецЦикла;	
	КонецЕсли;
	 
	// Обновляем ПВРСтроки ТЧ документа.
	Для Каждого стр Из РаботыДляЗаполненияПВР Цикл
		стрР = Работы[стр.НомерСтроки];
		стрР.ПВРСтроки = стр.ПВРСтроки;
	КонецЦикла;
КонецПроцедуры

Функция СоздатьПВРСтроки(знач СсылкаДляЗаписи, знач Дата, знач стр, знач КолонкиПоискаСоответствияПВР)
	СпрОб = Справочники.bz_ПозицияВыполненияРабот.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(СпрОб, стр, КолонкиПоискаСоответствияПВР);
	СпрОб.ВыполнениеРабот = СсылкаДляЗаписи;
	СпрОб.Дата = НачалоДня(Дата);
	СпрОб.Записать();
	Возврат СпрОб.Ссылка; 
КонецФункции
#КонецОбласти

#КонецЕсли
