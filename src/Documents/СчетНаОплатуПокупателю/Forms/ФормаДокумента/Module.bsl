
&НаКлиенте
Асинх Процедура ЗаполнитьВыставляемыеРаботы(Команда)
	Заголовок = НСтр("ru='Заполнение табличной части'"); 
	Если Объект.Проведен Тогда
		ПоказатьПредупреждение(НСтр("ru='Проведенный документ нельзя заполнять.'"),,, Заголовок);
		Возврат;
	КонецЕсли;
	Если Объект.Выработка.Количество() Тогда
		Ответ = Ждать ВопросАсинх(
			Нстр("ru='Табличная часть содержит строки и будет очищена. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет,,
			КодВозвратаДиалога.Нет,
			Заголовок
		);
			
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		Объект.bz_ВыставляемыеРаботы.Очистить();
	КонецЕсли;
	
	ЗаполнитьВыставляемыеРаботыСервер();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВыставляемыеРаботыСервер()
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	РаботыКВыставлениюОстатки.Проект КАК Проект,
	|	РаботыКВыставлениюОстатки.Этап КАК Этап,
	|	РаботыКВыставлениюОстатки.Работа КАК Работа,
	|	РаботыКВыставлениюОстатки.Сотрудник КАК Сотрудник,
	|	РаботыКВыставлениюОстатки.ПВР КАК ПВР,
	|	РаботыКВыставлениюОстатки.КоличествоСогласованоОстаток КАК Количество,
	|	РаботыКВыставлениюОстатки.СуммаСогласованоОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.bz_РаботыКВыставлению.Остатки(
	|			&Дата,
	|			Организация = &Организация) КАК РаботыКВыставлениюОстатки";
	
	Если не Объект.Контрагент.Пустая() Тогда
		Запрос.Текст=СтрЗаменить(Запрос.Текст, "Организация = &Организация", "Организация = &Организация
		|				И Проект В
		|					(ВЫБРАТЬ
		|						Т.Ссылка КАК Ссылка
		|					ИЗ
		|						Справочник.bz_Проекты КАК Т
		|					ГДЕ
		|						Т.КонтрагентРаботодатель = &Контрагент)");
		
		Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата)+1);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	ТЗ=Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	Объект.bz_ВыставляемыеРаботы.Загрузить(ТЗ);
КонецПроцедуры


&НаКлиенте
Процедура ПеренестиВТоварыИУслуги(Команда)
	ПеренестиВТоварыИУслугиВместоНаСервере();
КонецПроцедуры


&НаСервере
Процедура ПеренестиВТоварыИУслугиВместоНаСервере()
	ТЗ = Объект.bz_ВыставляемыеРаботы.Выгрузить(, "Работа, Количество, Сумма");
	ТЗ.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТЗ.Колонки.Добавить("Цена", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	МасРаботы = Новый Массив;
	Для каждого стр Из ТЗ Цикл
		Если стр.Количество = 0 Тогда
			стр.Цена = стр.Сумма;
		Иначе
			стр.Цена = стр.Сумма/стр.Количество;
		КонецЕсли;
		МасРаботы.Добавить(стр.Работа);
	КонецЦикла;
	
	НоменклатураРабот = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МасРаботы, "Номенклатура");
	Для каждого стр Из ТЗ Цикл
		стр.Номенклатура = НоменклатураРабот[стр.Работа];
	КонецЦикла;
	
	ТЗ.Свернуть("Номенклатура, Цена", "Количество, Сумма");
	Объект.Товары.Загрузить(ТЗ);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов, Склад, ДоговорКонтрагента,
		|ДокументБезНДС, Реализация, ПокупательНалоговыйАгентПоНДС, ВедетсяУчетНДСПоФЗ335");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	Если Не ИспользоватьТипыЦенНоменклатуры Тогда
		ПараметрыОбъекта.ТипЦен = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	ПараметрыОбъекта.Реализация = Истина;
	ПараметрыОбъекта.ВедетсяУчетНДСПоФЗ335 = ВедетсяУчетНДСПоФЗ335;
	ПараметрыОбъекта.ПокупательНалоговыйАгентПоНДС = ПокупательНалоговыйАгентПоНДС;

	Для каждого стр Из Объект.Товары Цикл
		ДанныеСтрокаТаблицы = Новый Структура("Номенклатура, Количество, Цена, Сумма, СтавкаНДС, СуммаНДС, ЭтоУслуга, СуммаСкидки, ПроцентСкидки");
		ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, стр);
		ТоварыНоменклатураПриИзмененииНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
		ЗаполнитьЗначенияСвойств(стр,ДанныеСтрокаТаблицы);

		Если стр.Свойство("Всего") Тогда
			стр.Всего = стр.Сумма + ?(Объект.СуммаВключаетНДС, 0, стр.СуммаНДС) - стр.СуммаСкидки;
		КонецЕсли;
	КонецЦикла;

	
КонецПроцедуры

